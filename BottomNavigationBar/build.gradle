plugins {
    id 'com.android.library'
    id 'maven-publish'      // 1. 添加 maven-publish插件
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 16
        targetSdk 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
}

// 2.配置插件参数
task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar // optional sources
            }
        }
    }
}

// 3. 如果能编译测试（gradlew publishToMavenLocal）成功，构建git: git update-index --chmod=+x gradlew